plugins {
    id "java"
    id "eclipse"
    id "maven-publish"
}

compileJava {
    sourceCompatibility = targetCompatibility = "21"
    options.compilerArgs << '--add-exports=java.desktop/sun.awt.image=ALL-UNNAMED'
}

version = ("0.0.1-" + ('git rev-parse --abbrev-ref HEAD'.execute().text.trim()) + "-" + ('git rev-parse --short HEAD'.execute().text.trim())).replace("/", "-")
group = "net.buildtheearth"

repositories {
    mavenCentral()
    maven {
        name = "JitPack"
        url = "https://jitpack.io/"
    }
    maven { // Required for leveldb
        name = "DaPorkchop_"
        url = "https://maven.daporkchop.net/"
    }
    maven { // Required for leveldb
        name = "OpenCollab Snapshots"
        url = "https://repo.opencollab.dev/snapshot/"
    }
    maven { // Required for LZMA
        name = "Sponge"
        url = "https://repo.spongepowered.org/maven"
    }
    maven { // For bukkit api bindings
        name = "PaperMC"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
}

dependencies {

    // The origins of the dependencies listed here correspond to the project that initially import them in Terra++.
    // If they are needed here, it's because Terra++ ultimately needs them as well.

    // Terra++ dependencies
    implementation "com.github.DaMatrix:commons-imaging:4c6c5dfe6401a884cb4cf53fb838c99e1dfb104c"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.18.0"
    implementation("net.daporkchop.lib:binary:0.5.7-SNAPSHOT")  {
        exclude group: "io.netty"
    }

    // Minecraft dependencies
    implementation "commons-codec:commons-codec:1.15"
    implementation "org.apache.commons:commons-lang3:3.12.0"
    implementation "it.unimi.dsi:fastutil:8.5.6"
    implementation "com.google.code.gson:gson:2.8.9"
    implementation "com.google.guava:guava:33.2.1-jre"
    implementation "io.netty:netty-all:4.1.111.Final"
    implementation "org.apache.logging.log4j:log4j-api:2.17.1"
    implementation "org.apache.logging.log4j:log4j-core:2.17.1"

    // Mixin dependencies
    implementation 'lzma:lzma:0.0.1'

    // Tests
    testImplementation "junit:junit:4.13.2"

    // Lombok
    compileOnly "org.projectlombok:lombok:1.18.32"
    annotationProcessor "org.projectlombok:lombok:1.18.32"

    // Bindings to other projects
    compileOnly "io.papermc.paper:paper-api:1.21-R0.1-SNAPSHOT"

}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = 'terraminusminus'
            version = project.version

            from components.java
        }
    }
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(21))
}

